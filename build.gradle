plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id "de.undercouch.download" version "4.1.2"
    id 'org.jreleaser' version '0.9.1'
}

apply from: "$rootDir/gradle/versioning.gradle"

repositories {
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
    mavenCentral()
}

asciidoctor {
    sourceDir  file('src/docs/asciidoc')
    outputDir  file('build/docs')
}

asciidoctorj {  // or asciidoctor.asciidoctorj
    attributes \
        revnumber : project.version,
        imagesdir : "images"
   options doctype: 'article', ruby: 'erubis'
}

pdfThemes {
    github 'sogis-frutiger', { 
        organisation = 'sogis' 
        repository = 'sogis-docs-asciidoctor-themes' 
        relativePath = 'sogis-frutiger' 
        branch = 'master' 
    }
}

asciidoctorPdf {
    attributes \
        imagesdir : "src/docs/asciidoc/images"
    theme 'sogis-frutiger'
    fontsDir file("fonts/")
}

task downloadFonts (type: Download) {
    src([
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-normal.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-bold.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-italic.ttf',
        'https://github.com/sogis/sogis-docs-asciidoctor-themes/raw/master/sogis-frutiger/fonts/frutiger-bold_italic.ttf'
    ])
    dest file("fonts/")
}

task deleteFonts(type: Delete) {
    delete 'fonts'
}

asciidoctorPdf.dependsOn downloadFonts
asciidoctorPdf.finalizedBy deleteFonts

jreleaser {
    project {
        name = 'dok-gis-schnittstellen'
        description = 'GIS-Schnittstellen'
        website = 'https://agi.so.ch'
        authors = ['edigonzales']
        license = 'MIT'
        docsUrl = 'https://github.com/sogis/dok-gis-schnittstellen'
    }
    release {
        github {
            owner = 'sogis'
            overwrite = true
            apiEndpoint = 'https://api.github.com'
        }
    }
    files {
        artifact {
            path = 'build/docs/asciidocPdf/gis-schnittstellen.pdf'
        }
    }
}
